plugins {
    id 'java'
    id 'com.modrinth.minotaur' version '2.+'
    id 'fabric-loom' version '1.10.2'
    id 'maven-publish'
}

version = mod_version
group = maven_group

base {
    archivesName = archives_base_name
}

loom {
    accessWidenerPath = file "src/main/resources/composer_reloaded.accesswidener"
}

repositories {
    maven {
        name "Ladysnake [CCA]"
        url "https://maven.ladysnake.org/releases"
    }
    maven {
        name "FzzyMaven [Fzzy Config]"
        url "https://maven.fzzyhmstrs.me/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:$minecraft_version"
    mappings "net.fabricmc:yarn:$yarn_mappings:v2"
    modImplementation "net.fabricmc:fabric-loader:$loader_version"
    modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_version"

    modImplementation "me.fzzyhmstrs:fzzy_config:$project.fzzy_version+$project.minecraft_version"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:$cca_version"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:$cca_version"
    include "dev.onyxstudios.cardinal-components-api:cardinal-components-base:$cca_version"
    include "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:$cca_version"
}

processResources {
    inputs.property "version", version
    inputs.property "minecraft_version", minecraft_version
    inputs.property "loader_version", loader_version
    inputs.property "cca_version", cca_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": version,
                "minecraft_version": minecraft_version,
                "loader_version": loader_version,
                "cca_version": cca_version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${archives_base_name}" }
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            from(components.java)

            groupId = group
            artifactId = archives_base_name
            version = version
        }
    }

    repositories {
        maven {
            name = "cloudsmith"
            url = "https://maven.cloudsmith.io/lilbrocodes/composer-reloaded/"
            credentials {
                username = System.getenv("CLOUDSMITH_USERNAME")
                password = System.getenv("CLOUDSMITH_API_KEY")
            }
        }
    }
}

modrinth {
    projectId = "composer-reloaded"
    uploadFile = remapJar
    versionName = "Composer Reloaded $version"
    gameVersions = [minecraft_version.toString()]
    dependencies {
        required.project "cardinal-components-api"
        required.project "fzzy-config"
    }
}

tasks.named('generatePomFileForMavenJavaPublication') {
    destination = file("pom.xml")
}
